////////////////////////////////////////////////////////////////////////////////
// Вспомогательная утилита получения файла AUTHORS из хранилища 1С
//

Перем Распаковщик;
Перем мПараметры;
Перем юТест;

Процедура ПодключитьРаспаковщик()
	
	Если Распаковщик = Неопределено Тогда
		ПодключитьСценарий("unpack.os", "Распаковщик");
		Распаковщик = Новый Распаковщик;
	КонецЕсли;
	
КонецПроцедуры

Процедура СгенерироватьФайлAUTHORS()
	
	ФайлХранилища = Новый Файл(мПараметры.ФайлХранилища);
	Если Не ФайлХранилища.Существует() Тогда
		ВызватьИсключение "Файл хранилища <" + ФайлХранилища.ПолноеИмя + "> не существует.";
	КонецЕсли;
	
	Каталог = мПараметры.ВыходнойКаталог;
	ФайлАвторов = Новый Файл(Каталог + "\AUTHORS");
	Если ФайлАвторов.Существует() Тогда
		Сообщить("Файл " + ФайлАвторов.ПолноеИмя + " уже существует. Пропускаем генерацию файла AUTHORS");
		Возврат;
	КонецЕсли;
	
	ПодключитьРаспаковщик();
	
	Попытка
		ПараметрыИнициализации = Распаковщик.ПолучитьПараметрыИнициализации();
		ПараметрыИнициализации.ДоменПочтыДляGit = мПараметры.ДоменПочты;
		Распаковщик.Инициализация(ПараметрыИнициализации);
		
		Сообщить("Формирую файл в каталоге " + Каталог);
		Распаковщик.СформироватьПервичныйФайлПользователейДляGit(ФайлХранилища.ПолноеИмя, ФайлАвторов.ПолноеИмя);
		Сообщить("Файл сгенерирован");
		
	Исключение
		Сообщить("ОШИБКА: Не удалось сформировать файл авторов");
		Распаковщик.УдалитьЗарегистрированныеВременныеФайлы();
		ВызватьИсключение;
	КонецПопытки;
	
	Распаковщик.УдалитьЗарегистрированныеВременныеФайлы();
	
КонецПроцедуры

Процедура СгенерироватьФайлVERSION()
	
	ПодключитьРаспаковщик();
	
	ФайлВерсий = Новый Файл(мПараметры.ВыходнойКаталог + "\VERSION");
	Если ФайлВерсий.Существует() Тогда
		Сообщить("Файл " + ФайлВерсий.ПолноеИмя + " уже существует. Пропускаем генерацию файла VERSION");
		Возврат;
	КонецЕсли;
	
	Распаковщик.ЗаписатьФайлВерсийГит(мПараметры.ВыходнойКаталог);
	
КонецПроцедуры

Процедура ПоказатьВозможныеАргументы()
	
	Сообщить("Для получения файла авторов из хранилища 1С задайте аргументы:
	|<ПутьКФайлуХранилища> <ДоменПочты> [ВыходнойКаталог]");
	
КонецПроцедуры

Процедура ПрочитатьПараметры()

	мПараметры = Новый Структура;
	мПараметры.Вставить("ФайлХранилища", АргументыКоманднойСтроки[0]);
	мПараметры.Вставить("ДоменПочты", АргументыКоманднойСтроки[1]);
	Если АргументыКоманднойСтроки.Количество() >= 3 Тогда
		Каталог = АргументыКоманднойСтроки[2];
	Иначе
		Каталог = ТекущийКаталог();
	КонецЕсли;
	мПараметры.Вставить("ВыходнойКаталог", Каталог);
	
КонецПроцедуры

Процедура ОбработатьПараметрыИВыполнитьЗадачу()

	Если АргументыКоманднойСтроки.Количество() < 2 Тогда	
		ПоказатьВозможныеАргументы();
		ЗавершитьРаботу(1);
	Иначе
		ПрочитатьПараметры();
		СгенерироватьФайлAUTHORS();
		СгенерироватьФайлVERSION();
	КонецЕсли;

КонецПроцедуры

/////////////////////////////////////////////////////////////////////////////////
// Юнит-тестирование

Функция ПолучитьСписокТестов(Тестирование) Экспорт
	
	юТест = Тестирование;
	
	СписокТестов = Новый Массив;
	СписокТестов.Добавить("Тест_СгенерироватьФайлАвторов");
	СписокТестов.Добавить("Тест_СгенерироватьФайлВерсий");
	
	Возврат СписокТестов;
	
КонецФункции

Процедура Тест_СгенерироватьФайлАвторов() Экспорт

	ИнициализироватьТестовыеПараметры();
	
	ФайлАвторов = Новый Файл("AUTHORS");
	юТест.ПроверитьЛожь(ФайлАвторов.Существует(), "Не должно быть файла AUTHORS в выходном каталоге");
	СгенерироватьФайлAUTHORS();
	юТест.ПроверитьИстину(ФайлАвторов.Существует(), "Должен быть файл AUTHORS в выходном каталоге");
	УдалитьФайлы(ФайлАвторов.ПолноеИмя);
	
КонецПроцедуры

Процедура Тест_СгенерироватьФайлВерсий() Экспорт
	
	ФайлАвторов = Новый Файл("VERSION");
	юТест.ПроверитьЛожь(ФайлАвторов.Существует(), "Не должно быть файла VERSION в выходном каталоге");
	СгенерироватьФайлVERSION();
	юТест.ПроверитьИстину(ФайлАвторов.Существует(), "Должен быть файл VERSION в выходном каталоге");
	УдалитьФайлы(ФайлАвторов.ПолноеИмя);
	
КонецПроцедуры

Процедура ИнициализироватьТестовыеПараметры()
	
	ПутьКФайлуХранилища1С = Новый Файл("testData\ТестовыйФайлХранилища1С.1CD");
	
	мПараметры = Новый Структура;
	мПараметры.Вставить("ФайлХранилища", ПутьКФайлуХранилища1С.ПолноеИмя);
	мПараметры.Вставить("ДоменПочты"   , "silverbulleters.org");
	мПараметры.Вставить("ВыходнойКаталог", ТекущийКаталог());
КонецПроцедуры

/////////////////////////////////////////////////////////////////////////////////
// Точка входа

Если СтартовыйСценарий().Источник = ТекущийСценарий().Источник Тогда	
	// Это реальный запуск, а не юнит-тестирование
	ОбработатьПараметрыИВыполнитьЗадачу();
КонецЕсли;
