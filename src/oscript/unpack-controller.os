
Перем Распаковщик;
Перем Параметры;

Процедура Синхронизировать()

	СоздатьРаспаковщик();
	
	Попытка
		Распаковщик.Инициализация(Распаковщик.ПолучитьПараметрыИнициализации());
		Если Не Распаковщик.СинхронизироватьХранилищеКонфигурацийСГит(Параметры) Тогда
			ВызватьИсключение "Синхронизация завершилась неудачно. См. лог";
		КонецЕсли;
	Исключение
		Распаковщик.УдалитьЗарегистрированныеВременныеФайлы();
		ВызватьИсключение;
	КонецПопытки;
	
	Распаковщик.УдалитьЗарегистрированныеВременныеФайлы();
	
КонецПроцедуры

Процедура ПрочитатьПараметры()
	Если АргументыКоманднойСтроки[0] = "-env" Тогда
		Параметры = ПолучитьПараметрыИзОкружения();
	Иначе
		Параметры = ПолучитьПараметрыИзКоманднойСтроки();
		Если Параметры = Неопределено Тогда
			ПоказатьВозможныеАргументы();
			ЗавершитьРаботу(1);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура СоздатьРаспаковщик()

	Если Распаковщик = Неопределено Тогда
		ПодключитьСценарий("unpack.os", "V83Unpack");
		Распаковщик = Новый V83Unpack;
	КонецЕсли;

КонецПроцедуры

Функция ПолучитьПараметрыИзОкружения()

	СИ = Новый СистемнаяИнформация();
	Окружение = СИ.ПеременныеСреды();
	
	Параметры = Новый Структура;
	Параметры.Вставить("ПутьКФайлуХранилища1С", Новый Файл(Окружение["storage_dir"]).ПолноеИмя + "\1cv8ddb.1CD");
	Параметры.Вставить("КаталогВыгрузки", Новый Файл(Окружение["output_dir"]).ПолноеИмя);
	
	Возврат Параметры;

КонецФункции

Функция ПолучитьПараметрыИзКоманднойСтроки()
	
	Если АргументыКоманднойСтроки.Количество() <> 2 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Параметры = Новый Структура;
	Параметры.Вставить("ПутьКФайлуХранилища1С", Новый Файл(АргументыКоманднойСтроки[0]).ПолноеИмя + "\1cv8ddb.1CD");
	Параметры.Вставить("КаталогВыгрузки", Новый Файл(АргументыКоманднойСтроки[1]).ПолноеИмя);
	
	Возврат Параметры;
	
КонецФункции

Процедура ПоказатьВозможныеАргументы()
	
	Сообщить("Для выгрузки конфигурации в CF задайте аргументы:
	|<ПутьКФайлуХранилища> <ПутьККаталогуGIT>");
	
	Сообщить("Для работы в окружении билд-сервера задайте ключ -env");
	
КонецПроцедуры

Если АргументыКоманднойСтроки.Количество() = 0 Тогда
	
	ПоказатьВозможныеАргументы();
	ЗавершитьРаботу(1);

Иначе
	
	ПрочитатьПараметры();
	Синхронизировать();
	
КонецЕсли;