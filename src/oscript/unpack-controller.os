
Перем Распаковщик;
Перем Параметры;

Процедура Синхронизировать()

	СоздатьРаспаковщик();
	Распаковщик.РежимОтладки(Истина);
	Попытка
		
		ПараметрыИнициализации = Распаковщик.ПолучитьПараметрыИнициализации();
		Если Параметры.Свойство("ПутьКПлатформе83") Тогда
			ПараметрыИнициализации.ПутьКПлатформе83 = Параметры.ПутьКПлатформе83;
		КонецЕсли;
		ПараметрыИнициализации.ПутьGit = Параметры.ПутьGit;
		
		Распаковщик.Инициализация(ПараметрыИнициализации);
		Если Не Распаковщик.СинхронизироватьХранилищеКонфигурацийСГит(Параметры) Тогда
			ВызватьИсключение "Синхронизация завершилась неудачно. См. лог";
		КонецЕсли;
	Исключение
		Распаковщик.УдалитьЗарегистрированныеВременныеФайлы();
		ВызватьИсключение;
	КонецПопытки;
	
	Распаковщик.УдалитьЗарегистрированныеВременныеФайлы();
	
КонецПроцедуры

Процедура ПрочитатьПараметры()
	Если АргументыКоманднойСтроки[0] = "-env" Тогда
		Параметры = ПолучитьПараметрыИзОкружения();
	Иначе
		Параметры = ПолучитьПараметрыИзКоманднойСтроки();
		Если Параметры = Неопределено Тогда
			ПоказатьВозможныеАргументы();
			ЗавершитьРаботу(1);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Функция АбсолютныйПуть(Знач ОтносительныйПуть)
	
	Каталог = Новый Файл(ТекущийСценарий().Источник).Путь;
	Возврат Каталог + "\" + ОтносительныйПуть;
	
КонецФункции

Процедура СоздатьРаспаковщик()

	Если Распаковщик = Неопределено Тогда
		ПодключитьСценарий(АбсолютныйПуть("unpack.os"), "V83Unpack");
		Распаковщик = Новый V83Unpack;
	КонецЕсли;

КонецПроцедуры

Функция ПолучитьПараметрыИзОкружения()

	СИ = Новый СистемнаяИнформация();
	Окружение = СИ.ПеременныеСреды();
	
	Параметры = Новый Структура;
	Параметры.Вставить("ПутьКФайлуХранилища1С", Новый Файл(Окружение["storage_dir"]).ПолноеИмя + "\1cv8ddb.1CD");
	Параметры.Вставить("КаталогВыгрузки", Новый Файл(Окружение["git_local_repo"]).ПолноеИмя);
	Параметры.Вставить("ПутьКПлатформе83", Окружение["v8_executable"]);
	Параметры.Вставить("ПутьGit", Окружение["git_executable"]);
	
	Возврат Параметры;

КонецФункции

Функция ПолучитьПараметрыИзКоманднойСтроки()
	
	Если АргументыКоманднойСтроки.Количество() <> 4 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Параметры = Новый Структура;
	Параметры.Вставить("ПутьКФайлуХранилища1С", Новый Файл(АргументыКоманднойСтроки[0]).ПолноеИмя + "\1cv8ddb.1CD");
	Параметры.Вставить("КаталогВыгрузки", Новый Файл(АргументыКоманднойСтроки[1]).ПолноеИмя);
	Параметры.Вставить("ПутьКПлатформе83", Новый Файл(АргументыКоманднойСтроки[2]).ПолноеИмя);
	Параметры.Вставить("ПутьGit", Новый Файл(АргументыКоманднойСтроки[3]).ПолноеИмя);
	
	Возврат Параметры;
	
КонецФункции

Процедура ПоказатьВозможныеАргументы()
	
	Сообщить("Для выгрузки конфигурации в CF задайте аргументы:
	|<ПутьКФайлуХранилища> <ПутьККаталогуGIT> <ПутьКПлатформе83> <ПутьGit>");
	
	Сообщить("Для работы в окружении билд-сервера задайте ключ -env");
	
КонецПроцедуры

Если АргументыКоманднойСтроки.Количество() = 0 Тогда
	
	ПоказатьВозможныеАргументы();
	ЗавершитьРаботу(1);

Иначе
	
	ПрочитатьПараметры();
	Синхронизировать();
	
КонецЕсли;