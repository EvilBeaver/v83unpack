
Перем Распаковщик;

Процедура ВыполнитьРаспаковку()
	
	Перем Параметры;
	
	Если АргументыКоманднойСтроки[0] = "-env" Тогда
		Параметры = ПолучитьПараметрыИзОкружения();
	Иначе
		Параметры = ПолучитьПараметрыИзКоманднойСтроки();
		Если Параметры = Неопределено Тогда
			ПоказатьВозможныеАргументы();
			ЗавершитьРаботу(1);
		КонецЕсли;
	КонецЕсли;
	
	ПодключитьСценарий("unpack.os", "V83Unpack");
	Распаковщик = Новый V83Unpack;
	
	Каталог = Новый Файл(Параметры.КаталогВыгрузки);
	Если Не Каталог.Существует() Тогда
		СоздатьКаталог(Каталог);
	ИначеЕсли Не Каталог.ЭтоКаталог() Тогда
		ВызватьИсключение "Путь <"+Параметры.КаталогВыгрузки+"> не является каталогом";
	ИначеЕсли Параметры.ОчищатьКаталог Тогда
		УдалитьФайлы(Параметры.КаталогВыгрузки, "*.*");
		Файлы = НайтиФайлы(Параметры.КаталогВыгрузки,"*.*");
		Если Файлы.Количество() Тогда
			ВызватьИсключение "Не удалось очистить выходной каталог";
		КонецЕсли;
		Сообщить("Выходной каталог очищен");
	КонецЕсли;
	
	Распаковщик.РазобратьФайлКонфигурации(Параметры.ПутьКФайлуКонфигурации, Параметры.КаталогВыгрузки);
	
КонецПроцедуры

Функция ПолучитьПараметрыИзОкружения()

	СИ = Новый СистемнаяИнформация();
	Окружение = СИ.ПеременныеСреды();
	
	Параметры = Новый Структура;
	Параметры.Вставить("ПутьКФайлуКонфигурации", Новый Файл(Окружение["config_file"]).ПолноеИмя);
	Параметры.Вставить("КаталогВыгрузки", Новый Файл(Окружение["output_dir"]).ПолноеИмя);
	Параметры.Вставить("ОчищатьКаталог", Истина);
	
	Возврат Параметры;

КонецФункции

Функция ПолучитьПараметрыИзКоманднойСтроки()
	
	Если АргументыКоманднойСтроки.Количество() < 2 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Параметры = Новый Структура;
	Параметры.Вставить("ПутьКФайлуКонфигурации", Новый Файл(АргументыКоманднойСтроки[0]).ПолноеИмя);
	Параметры.Вставить("КаталогВыгрузки", Новый Файл(АргументыКоманднойСтроки[1]).ПолноеИмя);
	Если АргументыКоманднойСтроки.Количество() > 2 и ВРЕГ(АргументыКоманднойСтроки[2]) = "ОЧИЩАТЬКАТАЛОГ" Тогда
		Параметры.Вставить("ОчищатьКаталог", Истина);
	Иначе
		Параметры.Вставить("ОчищатьКаталог", Ложь);
	КонецЕсли;
	
	Возврат Параметры;
	
КонецФункции

Процедура ПоказатьВозможныеАргументы()
	
	Сообщить("Для выгрузки конфигурации в CF задайте аргументы:
	|<ПутьКФайлуCF> <ПутьККаталогуВыгрузки> [ОчищатьКаталог]");
	
	Сообщить("Для работы в окружении билд-сервера задайте ключ -env");
	
КонецПроцедуры

Если АргументыКоманднойСтроки.Количество() = 0 Тогда
	
	ПоказатьВозможныеАргументы();
	ЗавершитьРаботу(1);

Иначе
	
	ВыполнитьРаспаковку();
	
КонецЕсли;