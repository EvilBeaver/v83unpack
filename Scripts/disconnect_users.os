
Перем мМенеджерСборки;

////////////////////////////////////////////////////////////////////////////
// Основная полезная нагрузка

Функция ОтключитьПользователей()
		
	мМенеджерСборки = Новый МенеджерСборки();
	СоединенияОтключены = Ложь;
	
	Попытка
		Менеджер = мМенеджерСборки.ПолучитьМенеджерКластера();
		Дескриптор = Менеджер.ДескрипторУправленияСеансамиБазы();
	Исключение
		СообщениеСборки(ИнформацияОбОшибке().Описание);
		Возврат СоединенияОтключены;
	КонецПопытки;
	
	Попытка
	
		Менеджер.ЗаблокироватьСоединенияСБазой(Дескриптор);
	
		Попытка
			ИнтервалОтключения = Число(Менеджер.Опция("ТаймаутБлокировки"));
		Исключение
			СообщениеСборки("Не удалось получить значение таймаута блокировки");
			ВызватьИсключение;
		КонецПопытки;
		
		МаксимальноеЧислоПопыток = 1;
		ЧислоПопыток = 0;
		Пока ЧислоПопыток < МаксимальноеЧислоПопыток Цикл
			
			Если Менеджер.ЕстьРаботающиеСеансы(Дескриптор, Истина) Тогда
				ЧислоПопыток = ЧислоПопыток + 1;
				СообщениеСборки("Есть работающие сеансы. Ждем " + ИнтервалОтключения + " секунд. Попытка №" + Строка(ЧислоПопыток));
				Приостановить(ИнтервалОтключения*1000);
			Иначе
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
		Если Менеджер.ЕстьРаботающиеСеансы(Дескриптор, Истина, Ложь) Тогда
			Менеджер.ПрекратитьСуществующиеСеансы(Дескриптор);
		КонецЕсли;
		
		Если Менеджер.ЕстьРаботающиеСеансы(Дескриптор, Истина, Ложь) Тогда
			ВызватьИсключение "Критичный сбой: Сеансы все равно не отключены!";
		КонецЕсли;
		
		СоединенияОтключены = Истина;
	Исключение
		Менеджер.ЗакрытьДескриптор(Дескриптор);
		СообщениеСборки(ИнформацияОбОшибке().Описание);
		СоединенияОтключены = Ложь;
	КонецПопытки;
	
	Менеджер.ЗакрытьДескриптор(Дескриптор);
	Если СоединенияОтключены Тогда
		СообщениеСборки("Соединения с базой " + Менеджер.Опция("ИмяБазы") + " успешно отключены");
	КонецЕсли;
	
	Возврат СоединенияОтключены;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////
// Служебные процедуры

Процедура СообщениеСборки(Знач Сообщение)

	мМенеджерСборки.СообщениеСборки(Сообщение);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////
// Точка входа в скрипт

ПодключитьСценарий("Scripts/build_main.os", "МенеджерСборки");

Если Не ОтключитьПользователей() Тогда
	СообщениеСборки("Отключение не выполнено. См. журнал сообщений");
	ЗавершитьРаботу(1);
КонецЕсли;